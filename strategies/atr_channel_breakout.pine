//@version=3
// 1. Define strategy settings
strategy(title="ATR Channel Breakout", overlay=true,
     pyramiding=0, initial_capital=100000,
     commission_type=strategy.commission.cash_per_order,
     commission_value=4, slippage=2)

smaLength = input(title="SMA Length", type=integer, defval=350)
atrLength = input(title="ATR Length", type=integer, defval=20)

ubOffset = input(title="Upperband Offset", type=float, defval=7, step=0.50)
lbOffset = input(title="Lowerband Offset", type=float, defval=3, step=0.50)

usePosSize = input(title="Use Position Sizing?", type=bool, defval=true)
riskPerc   = input(title="Risk %", type=float, defval=0.5, step=0.25)

// 2. Calculate strategy values
smaValue = sma(close, smaLength)
atrValue = atr(atrLength)

upperBand = smaValue + (ubOffset * atrValue)
lowerBand = smaValue - (lbOffset * atrValue)

riskEquity  = (riskPerc / 100) * strategy.equity
atrCurrency = (atrValue * syminfo.pointvalue)
posSize     = usePosSize ? floor(riskEquity / atrCurrency) : 1

// 3. Output strategy data
plot(series=smaValue, title="SMA", color=orange)

plot(series=upperBand, title="UB", color=green,
     linewidth=2)
plot(series=lowerBand, title="LB", color=red,
     linewidth=2)

// 4. Determine long trading conditions
enterLong = crossover(close, upperBand)
exitLong  = crossunder(close, smaValue)

// 5. Code short trading conditions
enterShort = crossunder(close, lowerBand)
exitShort  = crossover(close, smaValue)

// 6. Submit entry orders
if (enterLong)
    strategy.entry(id="EL", long=true, qty=posSize)

if (enterShort)
    strategy.entry(id="ES", long=false, qty=posSize)

// 7. Submit exit orders
strategy.close(id="EL", when=exitLong)
strategy.close(id="ES", when=exitShort)